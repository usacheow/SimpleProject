apply from: "$project.rootDir/dependencies.gradle"
apply from: "$project.rootDir/build_script/ktlint.gradle"

ext {

  applyAndroidApp = { module ->
    module.configure(module) {
      apply plugin: 'com.android.application'
    }
  }

  applyAndroidLibrary = { module ->
    module.configure(module) {
      apply plugin: 'com.android.library'
    }
  }

  applyAndroidCommon = { module ->
    module.configure(module) {
      apply plugin: 'kotlin-android'
      apply plugin: 'kotlin-kapt'

      android {

        compileSdkVersion androidCompileSdkVersion

        defaultConfig {
          minSdkVersion androidMinSdkVersion
          targetSdkVersion androidTargetSdkVersion

          vectorDrawables.useSupportLibrary = true
        }

        buildFeatures {
          viewBinding = true
        }

        kotlin {
          kapt {
            useBuildCache = true
          }
        }

        kotlinOptions {
          jvmTarget = "1.8"
        }

        compileOptions {
          coreLibraryDesugaringEnabled true

          sourceCompatibility JavaVersion.VERSION_1_8
          targetCompatibility JavaVersion.VERSION_1_8
        }
      }
    }

    module.dependencies {
      coreLibraryDesugaring coreLibraryDesugaringDependency

      implementation fileTree(include: ['*.jar'], dir: 'libs')
      implementation kotlinDependencies.values()
    }
  }

  applyDagger = { module ->
    module.configure(module) {
      apply plugin: 'dagger.hilt.android.plugin'
    }

    module.dependencies {
      implementation daggerDependency
      kapt kaptDaggerDependency

      implementation daggerViewModelDependency
      kapt kaptDaggerViewModelDependency
      kaptAndroidTest kaptDaggerViewModelDependency
    }
  }

  applyLifecycle = { module ->
    module.dependencies {
      implementation lifecycleDependency
      kapt kaptLifecycleDependency
    }
  }

  applyTest = { module ->
    module.configure(module) {
      android {
        defaultConfig {
          testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }
      }
    }

    module.dependencies {
      testImplementation project(':core_unit_test')
      androidTestImplementation uiTestDependencies.values()
    }
  }

}
