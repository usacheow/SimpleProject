apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'

apply from: "$project.rootDir/build_script/version.gradle"
apply from: "$project.rootDir/build_script/build_types.gradle"
apply from: "$project.rootDir/build_script/ktlint.gradle"
apply from: "$project.rootDir/dependencies.gradle"

android {

    compileSdkVersion androidCompileSdkVersion

    defaultConfig {
        applicationId applicationPackage
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode configVersionCode()

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    kotlin {
        kapt {
            useBuildCache = true
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile ConfigStoreFile
            storePassword ConfigStorePassword
            keyAlias ConfigKeyAlias
            keyPassword ConfigKeyPassword
        }
    }

    buildTypes {
        release {
            versionNameSuffix ReleaseSuffix
            signingConfig signingConfigs.release

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "APP_METRICA_KEY", '"SOME_VALUE"')
        }
        debug {
            versionNameSuffix DebugSuffix
            applicationIdSuffix DebugApplicationIdSuffix
            buildConfigField("String", "APP_METRICA_KEY", '"SOME_VALUE"')
        }
    }

    flavorDimensions "default"

    productFlavors {
        play {
            versionName configVersionName()
        }
        development {
            versionName configVersionName()
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation kotlinDependency

    implementation project(':core_mediator')
    implementation project(':core_data')
    implementation project(':core_ui')
    implementation project(':di')
    implementation project(':app_shared')

    implementation project(':feature_hello')
    implementation project(':feature_auth')
    implementation project(':feature_onboarding')

    implementation firebaseDependencies.values()
    implementation billingDependencies.values()

    implementation daggerDependency
    kapt kaptDaggerDependency
}
